##############################################################################
# cmake configuration file for DQM4hep
#
# returns following variables:
#
#   DQM4hep_FOUND      : set to TRUE if ALL components are found
#       if REQUIRED is NOT set components may or may not be available
#
#   DQM4hep_ROOT       : path to this DQM4hep installation
#   DQM4hep_VERSION    : package version
#   DQM4hep_LIBRARIES  : list of DQM4hep libraries (with all components)
#   DQM4hep_INCLUDE_DIRS  : list of paths to be used with INCLUDE_DIRECTORIES
#   DQM4hep_${COMPONENT}_FOUND       : set to TRUE or FALSE for each library
#   DQM4hep_${COMPONENT}_LIBRARIES   : path to individual libraries
#   DQM4hep_${COMPONENT}_INCLUDE_DIRS : path to individual includes
#
# @author: Remi Ete, DESY
##############################################################################

set( DQM4hep_DIR        "$ENV{DQM4hep_DIR}" )
set( DQM4hep_ROOT       "$ENV{DQM4hep_ROOT}" )
set( DQM4hep_VERSION    "@DQM4hep_VERSION@" )
set( DQM4hep_SOVERSION  "@DQM4hep_SOVERSION@" )
set( CMAKE_CXX_STANDARD  @CMAKE_CXX_STANDARD@ )
set( CMAKE_CXX_STANDARD_REQUIRED ON )
set( CMAKE_CXX_EXTENSIONS OFF )

set( ROOTSYS            "$ENV{ROOTSYS}" )
set( ROOT_DIR           "$ENV{ROOTSYS}/cmake" )
set( ROOT_VERSION       "@ROOT_VERSION@" )

set (CMAKE_MODULE_PATH  ${CMAKE_MODULE_PATH} ${DQM4hep_DIR}/cmake  )

#---------------------------------------------------------------------------------------------------
# Main functional include file
if ( "${DQM4hepBuild_included}" STREQUAL "" )
  include ( DQM4hepBuild )
endif()

# ---------- include dirs -----------------------------------------------------
# do not store find results in cache
set( DQM4hep_INCLUDE_DIRS DQM4hep_INCLUDE_DIRS-NOTFOUND )

mark_as_advanced( DQM4hep_INCLUDE_DIRS )

find_path( DQM4hep_INCLUDE_DIRS
  NAMES dqm4hep/DQM4hepConfig.h
  PATHS ${DQM4hep_DIR}/include
  NO_DEFAULT_PATH
)

# ---------- default build type  --------------------------------------------------------
if(NOT CMAKE_BUILD_TYPE AND NOT CMAKE_CONFIGURATION_TYPES)
  set (CMAKE_BUILD_TYPE RelWithDebInfo CACHE STRING "One of: None Debug Release RelWithDebInfo MinSizeRel." FORCE)
endif()

# ---------- libraries --------------------------------------------------------
# do not store find results in cache
set( DQM4hep_LIBRARY_DIRS ${DQM4hep_DIR}/lib )
set( DQM4hep_LIBRARIES    DQM4hep_LIBRARIES-NOTFOUND )
mark_as_advanced( DQM4hep_LIBRARY_DIRS DQM4hep_LIBRARIES )

include( ${DQM4hep_DIR}/cmake/DQM4hepMacros.cmake )

check_package_libs(DQM4hep DQMCore)

if(DQM4hep_FIND_COMPONENTS)
  message( STATUS "DQM4hep: Looking for Components: ${DQM4hep_FIND_COMPONENTS}" )

  foreach( comp ${DQM4hep_FIND_COMPONENTS} )
    #CHECK_PACKAGE_LIBS is looking for components too, just need to check if they were found, if requested
    string( TOUPPER ${comp} _ulibname )
    if( NOT DQM4hep_${_ulibname}_FOUND )
      message( FATAL_ERROR "Did not find required component: ${comp}" )
    endif()
  endforeach()
endif()

# ---------- final checking ---------------------------------------------------
include( FindPackageHandleStandardArgs )
# set DQM4HEP_FOUND to TRUE if all listed variables are TRUE and not empty
find_package_handle_standard_args( DQM4hep DEFAULT_MSG DQM4hep_DIR DQM4hep_INCLUDE_DIRS DQM4hep_LIBRARIES )

set( DQM4hep_FOUND ${DQM4HEP_FOUND} )

